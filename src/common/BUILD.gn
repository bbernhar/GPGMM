# Copyright 2019 The Dawn Authors
# Copyright 2021 The GPGMM Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../build_overrides/gpgmm_overrides_with_defaults.gni")

import("//build_overrides/build.gni")
import("${gpgmm_root_dir}/build_overrides/gpgmm_features.gni")

# Use Chromium's dcheck_always_on when available so that we respect it when
# running tests on the GPU builders
if (build_with_chromium) {
  import("//build/config/dcheck_always_on.gni")
} else {
  dcheck_always_on = false
}

if (build_with_chromium) {
  import("//build/config/sanitizers/sanitizers.gni")
} else {
  use_fuzzing_engine = false
}

###############################################################################
# Common gpmm configs
###############################################################################

config("gpgmm_public_include_dirs") {
  defines = []
  if (gpgmm_enable_d3d12) {
    defines += [ "GPGMM_ENABLE_D3D12=1" ]
  } else {
    defines += [ "GPGMM_ENABLE_D3D12=0" ]
  }

  include_dirs = [
    "${gpgmm_root_dir}/",
    "${gpgmm_root_dir}/src/include",
  ]
}

config("gpgmm_internal") {
  include_dirs = [ "${gpgmm_root_dir}/" ]

  defines = []
  if (gpgmm_always_assert || dcheck_always_on || is_debug ||
      use_fuzzing_engine) {
    defines += [ "DAWN_ENABLE_ASSERTS" ]
  }

  if (use_fuzzing_engine) {
    # Does a hard abort when an assertion fails so that fuzzers catch and parse the failure.
    defines += [ "DAWN_ABORT_ON_ASSERT" ]
  }

  # Only internal Dawn targets can use this config, this means only targets in
  # this BUILD.gn file and related subdirs.
  visibility = [ "../*" ]

  cflags = []

  # Enable more warnings that were found when using Dawn in other projects.
  # Add them only when building in standalone because we control which clang
  # version we use. Otherwise we risk breaking projects depending on Dawn when
  # the use a different clang version.
  if (gpgmm_standalone && is_clang) {
    cflags += [
      "-Wconditional-uninitialized",
      "-Wcstring-format-directive",
      "-Wc++11-narrowing",
      "-Wdeprecated-copy",
      "-Wdeprecated-copy-dtor",
      "-Wduplicate-enum",
      "-Wextra-semi-stmt",
      "-Wimplicit-fallthrough",
      "-Winconsistent-missing-destructor-override",
      "-Winvalid-offsetof",
      "-Wmissing-field-initializers",
      "-Wnon-c-typedef-for-linkage",
      "-Wpessimizing-move",
      "-Wrange-loop-analysis",
      "-Wredundant-move",
      "-Wshadow-field",
      "-Wstrict-prototypes",
      "-Wtautological-unsigned-zero-compare",
      "-Wunreachable-code-aggressive",
    ]

    # Allow comparison against type limits that might be tautological on 32bit
    # or 64bit systems. Without this the following produces an error on 64bit:
    #
    #   if (myUint64 > std::numeric_limits<size_t>::max()) {...}
    cflags += [ "-Wno-tautological-type-limit-compare" ]

    if (is_win) {
      cflags += [
        # clang-cl doesn't know -pedantic, pass it explicitly to the clang driver
        "/clang:-pedantic",

        # Allow the use of __uuidof()
        "-Wno-language-extension-token",
      ]
    } else {
      cflags += [ "-pedantic" ]
    }
  }

  if (!is_clang && is_win) {
    # Dawn extends wgpu enums with internal enums.
    # MSVC considers these invalid switch values. crbug.com/dawn/397.
    cflags += [ "/wd4063" ]
    if (gpgmm_is_winuwp) {
      # /ZW makes sure we don't add calls that are forbidden in UWP.
      # and /EHsc is required to be used in combination with it,
      # even if it is already added by the windows GN defaults,
      # we still add it to make every /ZW paired with a /EHsc
      cflags_cc = [
        "/ZW:nostdlib",
        "/EHsc",
      ]
    }
  }

  # TODO: DML support
  # if (is_clang && webnn_enable_dml) {
  #   cflags += [
  #     # Allow the use of DEFINE_ENUM_FLAG_OPERATORS(ENUMTYPE); in DirectML.h
  #     "-Wno-extra-semi",

  #     # Allow the use of DMLX_THROW(_hr); in DirectMLX.h
  #     "-Wno-extra-semi-stmt",

  #     # Allow the use of DML_OPERATOR_GRAPH_NODE_DESC{ node.op.Get() } in DirectMLX.h
  #     "-Wno-missing-field-initializers",
  #   ]
  # }
}

###############################################################################
# Common GPGMM library
###############################################################################

# This GN file is discovered by all Chromium builds, but common doesn't support
# all of Chromium's OSes so we explicitly make the target visible only on
# systems we know Dawn is able to compile on.
if (is_win || is_linux || is_chromeos || is_mac || is_fuchsia || is_android) {
  static_library("common") {
    sources = [
      "Assert.cpp",
      "Assert.h",
      "Compiler.h",
      "IntegerTypes.h",
      "Limits.h",
      "LinkedList.h",
      "Log.cpp",
      "Log.h",
      "Math.cpp",
      "Math.h",
      "Platform.h",
      "SystemUtils.cpp",
      "SystemUtils.h",
    ]

    public_configs = [ ":gpgmm_internal" ]

    # deps = [ "${gpgmm_root_dir}/src/gpgmm:gpgmm_headers" ]
    if (is_android) {
      libs = [ "log" ]
    }
  }
}
